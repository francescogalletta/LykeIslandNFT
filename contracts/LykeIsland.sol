// SPDX-License-Identifier: MIT
// written by Goldmember@0001

//MMMMMMMMMMMMMMMMMMMMNXNMMMMMMMMMMMMMMWWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMWd':0MMMMMMMMMMMMNdc0MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMNc  'OMMMMMMMMMMWd. dWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMNc ..;KMMMMMMMMMO'  oWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMNc.::.lNMMMMMMMK,   oWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMWd.;o,.xWMMMMMNc.'. lWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMO''lc.,0MMMMMk..c,.oWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMX:.co;.lNMMMX:.:o'.dWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMWx.,ol..kMMMk.'ol..xMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMX:.co:.:XMNl.;oc..OMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMWk.'ll..xWK,.co:.,0MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMX:.:o:..c;.'lo;.cNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMk..loc;'.'coo,.dWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMX:.:ooooooooo'.xMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMWo.,l;'cc,,cl'.kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMWd..:. ,;  ,l..kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMW0; .c;.;c..,l..kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMWKo..,cooooolcll..kMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMWKo..,coollloooodl..xMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMWKo..'clc;'..'cooool. lWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMXo.  .;'.':dl..cooooo' ;KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMWo     .;xKWMO'.loooool'.:0WWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMWO:',cdKWMMMWx.'ooodddoo:.';lKWMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMWNWMMMMMMMK;.;xOO0Kkxdol:,''oKMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMWo..l0NNNNKkdooooo:.'dXMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMO'.:kXNWWWXOdooodool'.;0MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMNc.;okXNNNNKxooooooool,.;KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMM0'.coddc;lkkl;:o:',:ool,.oNMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMx.'oo,... ,l,.,c. .:oooc.,KMMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMWo.;o:. .. ,c..:;. .coool.'0MMMMMMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMNl.;o:..'..:c..c:. .loool.'OWWNNWMMMMMMMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMX:.:o:... .c:..cl. .:oooc. .,,,,;:lx0NWMMMMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMM0'.cd;... .l; .lo;...:oo;. .',;;;,'..,cxKWMMMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMk..lo,    ,o,  ..... 'lc. ....,cooooc:,.'l0WMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMXc .l:..;. ;o, .,.   .,'..:kOOd'.,;;cdxxxl..xWMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMNo. .'..xK; ;l' c0l''';:lxKWMMMWk,.. .oNNWWd..kMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMXl.  .'cOKo..::..xMWNNNWWMMMMMMMMWXK0: ,dd0WX:.lWMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMNxccdOXWO,   ..'oXMMMMMMMMMMMMMMMMMMMk.   ;KWd.oWMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMWO:;cox0NWMMMMMMMMMMMMMMMMMMMM0;   '0Nc.xMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMWWMMMMMMMMMMMMMMMMMMMMMMMMMMWXx' ,ko.:KMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWx'';;dXMMMMMMMMMMMMMM
//MMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMMWX0KNWMMMMMMMMMMMMMMM

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/Counters.sol";

contract LykeIslandNFT is ERC721, Ownable {

    // util tokens and strings
    using Counters for Counters.Counter;
    Counters.Counter private _nextTokenId;

    // URI and toggle
    string public baseURI;
    bool public isURIlocked = false;

    // prices and supply
    // [TODO] Review with Ash
    uint256 public price = 0.833 ether;
    uint256 public constant maxSupply = 120;
    uint256 public constant allowlistMintSupply = 112;
    uint256 public constant reservedMintSupply = 8;
    uint256 public constant mintPerAllowlistAddress = 1;
    uint256 public constant mintsForDev = 1;
    uint256 public constant mintsForFounders = 2;
    uint256 public constant mintsForCommunity = 5;

    // sales constants
    // [TODO] consider doing a timed sale versus toggling
    bool public isAllowlistMintActive = false;
    bool public isReservedMintActive = true;

    // allowlist address + minted balance
    // [TODO] review if to use merkle tree and replace allowlist map
    mapping(address => uint256) public allowlistMintAllowance;
    uint256 public allowlistSize;

    // team and community wallets for reserve mint
    address public devWallet = 0x5056F17776CD4682B8961C8AC26C0b420009B6c7;
    address public foundersWallet = 0xBdD93FA3ff5AfF250650a1E3622B224bD74BD2E5;
    address public communityWallet = 0xBdD93FA3ff5AfF250650a1E3622B224bD74BD2E5;

    constructor(
        string memory _uri // i.e. https://77.7.777/
    ) ERC721("LykeIsland", "LYKEISLAND") {

        _nextTokenId.increment();

        baseURI = _uri;
    }

    function addToAllowlist(address[] memory _allowlistEntries) external onlyOwner {
        require(_allowlistEntries.length + allowlistSize <= allowlistMintSupply, "LYKE: can not add this many to allowlist");
        for (uint256 i = 0; i < _allowlistEntries.length; i++) {
            allowlistMintAllowance[_allowlistEntries[i]] = mintPerAllowlistAddress;
            allowlistSize++;
        }
    }

    function _baseURI() internal view override returns (string memory) {
        return baseURI;
    }

    function setBaseURI(string calldata uri) external onlyOwner {
        require(!isURIlocked, "LYKE: URI is locked now, can not be changed");
        baseURI = uri;
    }

    function toggleURIlock() external onlyOwner {
        isURIlocked = !isURIlocked;
    }

    // generic mint function, iterate over amount and safeMints to same target
    function _mintUnits(address _to, uint256 _amount) private {
        require(_amount + _nextTokenId.current() <= maxSupply);
        for (uint256 i = 0; i < _amount; i++) {
            _safeMint(_to, _nextTokenId.current());
            
            _nextTokenId.increment();
        }
    }

    // minting for the founding team and community directly to wallets
    function reserveMint() external onlyOwner {
        require(isReservedMintActive, "LYKE: reserved mint is no longer active");
        // function can be used exactly 1 time, there is no toggle function for reserved mint
        isReservedMintActive = !isReservedMintActive;

        // founders mint
        _mintUnits(foundersWallet, mintsForFounders);

        // community wallet mint
        _mintUnits(communityWallet, mintsForCommunity);

        // dev mint
        _mintUnits(devWallet, mintsForDev);
    }

    function toggleAllowlistMint() public onlyOwner {
        isAllowlistMintActive = !isAllowlistMintActive;
    }

    // allowlist mint
    // no amount parameter as it is a max of 1 per wallet
    function allowlistMint() external payable {
        require(isAllowlistMintActive, "LYKE: allowlist mint is not active");
        require(msg.sender == tx.origin, "LYKE: minter can not be a contract");
        require(allowlistMintAllowance[msg.sender] >= mintPerAllowlistAddress, "LYKE: not in allowlist or max minted");
        require(msg.value >= price , "LYKE: insufficient amount paid to mint");

        // mints the allowed amount 
        allowlistMintAllowance[msg.sender] = 0;
        _mintUnits(msg.sender, 1);
    }

    function withdrawBalance() external onlyOwner {
        (bool success, ) = msg.sender.call{value: address(this).balance}("");
        require(success, "LYKE: withdrawal failed");
    }

}
